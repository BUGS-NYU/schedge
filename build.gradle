buildscript {
    ext.kotlin_version = '1.3.50'
    ext.serialization_version = '0.4'
}

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id "application"
    id "com.github.johnrengelman.shadow" version "5.1.0"
    id 'nu.studer.jooq' version '3.0.3'
    id "org.flywaydb.flyway" version "6.1.0"
}

group 'BUGS-NYU'
mainClassName = 'Main'
sourceCompatibility = 1.8

def getenv = { env_val, default_val ->
  ext.val = System.getenv(env_val)
  if (ext.val == null) {
    return default_val
  } else return ext.val
}

def dbUsername = getenv("DB_USERNAME", "schedge")
def dbPassword = getenv("DB_PASSWORD", "docker")
def dbUrl = getenv("JDBC_URL", "jdbc:postgresql://localhost:5432/schedge")
def dbDriver = 'org.postgresql.Driver'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation(
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8", // Standard library

            "io.github.microutils:kotlin-logging:1.7.6", // Logging
            "org.slf4j:slf4j-api:1.7.25", // Logging
            "ch.qos.logback:logback-core:1.2.3", // Logging
            "ch.qos.logback:logback-classic:1.2.3", // Logging

            "javax.annotation:javax.annotation-api:1.2-b01", // Annotations

            "joda-time:joda-time:2.10.5", // Datetime

            "org.postgresql:postgresql:42.2.8.jre7", // Database driver
            "com.zaxxer:HikariCP:3.4.1", // Database connection pool
            "org.jooq:jooq", // Database framework
            "org.jooq:jooq-meta", // Database framework
            "org.jooq:jooq-codegen", // Database framework

            "com.github.kittinunf.fuel:fuel:2.2.1", // Async web requests framework

            "org.jsoup:jsoup:1.12.1", // HTML Parsing

            "info.picocli:picocli:4.1.4", // Command line args

            "com.fasterxml.jackson.core:jackson-core:2.4.1", // JSON
            "com.fasterxml.jackson.core:jackson-annotations:2.4.1", // JSON
            "com.fasterxml.jackson.core:jackson-databind:2.4.1", // JSON
            "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.9", // JSON

            "io.javalin:javalin:3.5.0", // Web server framework
            "io.swagger.core.v3:swagger-core:2.0.8", // Web server docs framework
            "cc.vileda:kotlin-openapi3-dsl:0.20.1", // Web server docs DSL framework
    )

    jooqRuntime 'postgresql:postgresql:9.1-901.jdbc4'

    testImplementation(
            "io.kotlintest:kotlintest-runner-junit5:3.3.2"
    )

    // standalone "org.junit.platform:junit-platform-console-standalone:1.5.2"
}

flyway {
  driver = dbDriver
  url = dbUrl
  user = dbUsername
  password = dbPassword
  locations = ['filesystem:migrations']
}

jooq {
  version = '3.11.9'
  edition = 'OSS'

  database(sourceSets.main) {
    jdbc {
      driver = dbDriver
      url = dbUrl
      user = dbUsername
      password = dbPassword
    }
    generator {
      database {
        name = 'org.jooq.meta.postgres.PostgresDatabase'
        includes = '.*'
        excludes = ''
        inputSchema = 'public'
      }

      generate {
        relations = true
        deprecated = false
        records = true
        immutablePojos = true
        fluentSetters = true
      }

      target {
        packageName = 'database.generated'
        directory = 'src/main/java'
      }
    }
  }
}

// Set up Jooq to always codegen when needed, but not any other time
project.tasks.getByName('compileJava').dependsOn -= 'generateDatabaseJooqSchemaSource'
project.tasks.getByName('generateDatabaseJooqSchemaSource').outputs.upToDateWhen { false }

// The build directory should not begin with the same name as the script
allprojects {
    buildDir = ".build"
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task checkFast {}
checkFast.dependsOn += 'compileJava'
checkFast.dependsOn += 'compileKotlin'

task updateDb {}
updateDb.dependsOn += 'flywayClean'
updateDb.dependsOn += 'flywayMigrate'
updateDb.dependsOn += 'generateDatabaseJooqSchemaSource'



